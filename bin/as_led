#!/usr/bin/env zsh

typeset -A leds=(
	sleep	20
	usb	34
	status	47
	lan	77
	power	80
)

typeset -a led_on
typeset -a led_off

typeset turn_on=1
if [[ $2 == 0 ]] || [[ $2 = off ]]; then
	turn_on=0
fi

case $1 in
	brightness)
		print $2 > ${$(grep -l it8728 /sys/class/hwmon/hwmon0/name):h}/pwm3
		exit 0
		;;
	sleep)
		led_on=(sleep)
		if ((turn_on)); then
			led_off=(usb status power)
			led_on+=(lan)
		fi
		;;
	usb)
		led_on=(usb)
		;;
	status)
		led_on=(status)
		;;
	lan)
		led_off=(lan) # Reverse.
		;;
	power)
		led_on=(power)
		((turn_on)) && led_off=(sleep)
		;;
esac

typeset on=0 off=1
if ((!turn_on)); then
	on=1 off=0
fi

for led in $led_on; do
	it87_output ${${leds[$led]}[1]} ${${leds[$led]}[2]} $on
done

for led in $led_off; do
	it87_output ${${leds[$led]}[1]} ${${leds[$led]}[2]} $off
done
